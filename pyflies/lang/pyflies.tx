/*
  This is a textX specification of pyFlies DSL for Reaction Time test
  experiments definition.
  Author: Igor R. Dejanovic <igor DOT dejanovic AT gmail DOT com>
  Copyright: (c) 2014 Igor R. Dejanovic <igor DOT dejanovic AT gmail DOT com>
  License: MIT License
*/

PyFliesModel:
  ("basepath" basepath=STRING)?
  elements+=ModelElement
;

ModelElement:
  TestDef|Experiment|Text|Subject
;

TestDef:
  "test" name=ID (":" inherits*={[TestDef] ","} )? "{"
    ( ("tmin" "=" tmin=INT)|
    ("tmax" "=" tmax=INT)|
    ("twait" "=" twait=INT)|
    (randomize?="randomize")|
    (practice?="practice")|
    ("trials" "=" trials=INT)|
    ("stimuli" "=" "[" stimuli+={Stimulus ","} "]")|
    ("correctResponses" "=" "[" correctResponses+={INT ","} "]")|
    ("keyboard" "=" "[" keys +={Key ","} "]")|
    ("mouse" "=" "[" mouse +={STRING ","} "]") )*
  "}"
;

Stimulus:
  file = STRING
  (category = ID)?
;

Key:
  "ANY"|"UP"|"DOWN"|"LEFT"|"RIGHT"
;

Mouse:
  "ANY"|STRING
;

Block:
  Sequence|Randomize
;

TestReference:
  'test' test=[TestDef]
;

TextReference:
  'text' text=[Text]
;

SubjectReference:
  'subject' subject=[Subject]
;

Reference:
  TestReference|TextReference|SubjectReference
;

ExperimentElement:
  Reference|Block
;


Experiment:
  'experiment' '{'
    elements*=ExperimentElement
  '}'
;

Sequence:
  'sequence' '{'
    elements*=ExperimentElement
  '}'
;

Randomize:
  'randomize' '{'
    elements*=ExperimentElement
  '}'
;

Text:
  'text' name=ID "{"
    ( ('content'
      content=/(.|\n)*?(?=endcontent)/
    'endcontent')|
    ("keyboard" "=" "[" keys +={Key ","} "]")|
    ("mouse" "=" "[" mouse +={Mouse ","} "]") )*
  '}'
;

Subject:
  'subject' name=ID '{'
    attribute+=SubjectAttribute
  '}'
;

SubjectAttribute:
  type=SubjectAttributeType name=ID (label=STRING)?
;

SubjectAttributeType:
  "int"|"string"|"float"|Enum
;

Enum:
  '[' values+={ID ','} ']'
;

// Special rule for comments
Comment:
  /\/\/.*$/|/\/\*(.|\n)*?\*\//  // Non-greedy match of block component content
;

