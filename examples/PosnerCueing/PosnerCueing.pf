experiment "Posner Cueing"
"
A neuropsychological test often used to assess attention.

See also:
http://en.wikipedia.org/wiki/Posner_cueing_task
"

test Posner {
	conditions {
		cue     position	congruency		response

		left	left		congruent		press
		left	right		incongruent		press
		right	left		incongruent		press
		right	right		congruent		press
	}
	
	stimuli{
		all: 	shape(cross, duration 1, keep)
				shape(rectangle, duration 1, position left, size 22, keep)
				shape(rectangle, duration [1000, 1500], position right, size 22, keep)
				shape(rectangle, duration [1000, 1500], position cue, 
					  size 20, keep)
				shape(cross, target, position position)
		error: 	sound(1000, duration 300)
	}
}

screen Practice {
  Posner Cueing
  -------------
	
  You will be presented with the two empty boxes positioned left and right
  from the center of the screen. You should concentrate on the center of the screen
  and wait for the cue. The cue will be presented as a box highlight.
  After some time a cross shaped object will be presented in some of the squares.
  React as fast as you can by pressing space button when you detect a cross shaped object.
	
  Press SPACE for practice block.
}

screen Real {
  Posner Cueing
  -------------
	
  Now a REAL testing will be performed.

  Press SPACE to start the test.
	
}

structure {
	screen Practice
	test Posner 1 practice randomize
	screen Real
	test Posner 1 randomize
}

/*
target Expyriment {
  output = "/home/igor/tmp/Simon/"
  responses {
  	// see expyriment/misc/constants.py
    left = K_LEFT
    right = K_RIGHT
  }
}
*/

target PsychoPy {
  output = "/home/igor/tmp/PosnerPsycho/"
  responses {
    press = space
  }
}
