test timings{
  tmin = 3000
  tmax = 5000
  twait = 2000
  randomize
}

test EriksenFlankerBase: categorisation, visual, timings {
            // filename  category(optional)
  stimuli = ['file1.png' incongruent,
             'file2.png' congurent,
             'file3.png' congurent]

  correctResponses = [1, 3]                   // Index in the stimuli array
                                            // this param is optional. If given experiment executor can register
                                            // weather response was right or wrong.
  keyboard = [LEFT, RIGHT]

          // Button images or labels
  mouse = ["left_arrow.png", "Right"]
}

test EriksenFlanker5: EriksenFlankerBase {
  practice
  trials = 5
}

test EriksenFlanker30: EriksenFlankerBase {
  trials = 30
}

text intro {
  content
    Welcome
    -------
    In the following test you will be presented with a line of 5 arrow pointing
    left or right like in this picture:

    .. image:: left_incongruent.png

    You should respond by the direction of the *middle arrow* as fast as possible
    by pressing left or right arrow on the keyboard or touching/clicking
    on appropriate button on the screen.

    You will first do a practice run with 5 trials. After that a real run with
    30 trial will be performed.

    Press any key or touch/click the screen to continue
  endcontent

  keyboard = [ANY]
  mouse = [ANY]
}

subject subject_data {
  string full_name "First and last name"
  int age
  [Male, Female] gender "Gender"
}


experiment {

  // First collect information about subject
  subject subject_data

  // Show introduction
  text intro

  // Perform practice test serie
  test EriksenFlanker5

  // Perform real test serie (collects data)
  test EriksenFlanker30

}


